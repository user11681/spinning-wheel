allprojects {
    plugins.with {
        apply("maven-publish")
        apply("java-library")
    }

    group = "user11681"
    version = "0-SNAPSHOT"

    sourceCompatibility = 16
    targetCompatibility = 16

    if (project != rootProject) {
        ext.loader = name.replace("wheel-", "")
    }

    sourceSets {
        main {
            java.srcDir(rootProject.file("../src/${project == rootProject ? "common" : loader}"))
        }
    }

    repositories {
        mavenCentral()

        maven {url = "https://auoeke.jfrog.io/artifactory/maven"}
    }

    dependencies {
        api(gradleApi())

        api("net.gudenau.lib:unsafe:latest.integration")
        api("user11681:reflect:latest.integration")
        api("user11681:uncheck:latest.integration")
        api("org.ow2.asm:asm:latest.integration")
        api("org.ow2.asm:asm-tree:latest.integration")

        compileOnly("org.jetbrains:annotations:latest.integration")
    }

    java {
        withSourcesJar()
    }

    jar {
        from(rootProject.file("../LICENSE"))
    }

    publishing {
        repositories {
            mavenLocal()

            maven {
                url = "https://auoeke.jfrog.io/artifactory/maven"

                credentials {
                    username = System.properties["maven.username"]
                    password = System.properties["maven.password"]
                }
            }
        }
    }
}

subprojects {
    plugins.apply("java-gradle-plugin")

    dependencies {
        api(rootProject)
    }

    gradlePlugin {
        plugins {
            register(project.name) {
                id = project.name
                implementationClass = "user11681.wheel.Wheel${project.loader.capitalize()}Plugin"
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}
