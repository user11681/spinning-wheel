allprojects {
    plugins.apply("maven-publish")
    plugins.apply("java-library")

    group = "user11681"
    version = "0-SNAPSHOT"

    sourceCompatibility = 16
    targetCompatibility = 16

    repositories {
        mavenCentral()

        maven {url = "https://auoeke.jfrog.io/artifactory/maven"}
    }

    dependencies {
        api(gradleApi())

        api("net.gudenau.lib:unsafe:latest.integration")
        api("user11681:reflect:latest.integration")
        api("user11681:uncheck:latest.integration")
        api("org.ow2.asm:asm:latest.integration")
        api("org.ow2.asm:asm-tree:latest.integration")

        compileOnly("org.jetbrains:annotations:latest.integration")
    }

    java {
        withSourcesJar()
    }

    jar {
        from(rootProject.file("../LICENSE"))
    }

    publishing {
        repositories {
            mavenLocal()

            maven {
                url = "https://auoeke.jfrog.io/artifactory/maven"

                credentials {
                    username = System.properties["maven.username"]
                    password = System.properties["maven.password"]
                }
            }
        }
    }

    sourceSets {
        main {
            resources.srcDirs.clear()
            java.srcDirs = [rootProject.file("../src/${project == rootProject ? "common" : project.name}")]
        }
    }
}

subprojects {
    plugins.apply("java-gradle-plugin")

    dependencies {
        api(rootProject)
    }

    gradlePlugin {
        plugins {
            register(project.name) {
                id = "wheel-${project.name}"
                implementationClass = "user11681.wheel.Wheel${project.name.capitalize()}Plugin"
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}
