allprojects {
    plugins.with {
        apply("maven-publish")
        apply("java-library")
    }

    group = "net.auoeke"
    version = "0-SNAPSHOT"

    sourceCompatibility = 16
    targetCompatibility = 16

    sourceSets {
        main {
            java.srcDir(rootProject.file("../src/${project == rootProject ? "common" : project.type}"))
        }
    }

    repositories {
        mavenCentral()

        maven {url = "https://auoeke.jfrog.io/artifactory/maven"}
    }

    dependencies {
        api(gradleApi())

        api("net.gudenau.lib:unsafe:latest.integration")
        api("user11681:reflect:latest.integration")
        api("user11681:uncheck:latest.integration")
        api("org.ow2.asm:asm:latest.integration")
        api("org.ow2.asm:asm-tree:latest.integration")

        compileOnly("org.jetbrains:annotations:latest.integration")
    }

    java {
        withSourcesJar()
    }

    jar {
        from(rootProject.file("../LICENSE"))
    }

    publishing {
        repositories {
            mavenLocal()

            maven {
                url = "https://auoeke.jfrog.io/artifactory/maven"

                credentials {
                    username = System.properties["maven.username"]
                    password = System.properties["maven.password"]
                }
            }
        }
    }

    if (plugin) {
        plugins.apply("java-gradle-plugin")

        gradlePlugin {
            plugins {
                register(project.name) {
                    id = project.name
                    implementationClass = "net.auoeke.wheel.Wheel${Arrays.stream(project.type.split("-")).map{it.capitalize()}.toArray().join()}Plugin"
                }
            }
        }
    } else {
        publishing {
            publications {
                maven(MavenPublication) {
                    from(components.java)
                }
            }
        }
    }
}

subprojects {
    dependencies {
        api(rootProject)
    }
}
