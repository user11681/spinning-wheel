import java.lang.management.ManagementFactory

plugins {
    id("org.jetbrains.kotlin.jvm").version("latest.release")
}

allprojects {
    plugins.with {
        apply("maven-publish")
        apply("java-library")
        apply("org.jetbrains.kotlin.jvm")
    }

    group = "net.auoeke"
    version = "0-SNAPSHOT"

    sourceSets {
        main {
            java.srcDir("src")
            kotlin.srcDir("src")
        }
    }

    repositories {
        mavenCentral()

        maven {url = "https://maven.auoeke.net"}
    }

    dependencies {
        api(gradleApi())
        api("net.auoeke:extensions:latest.integration")
        api("net.auoeke:reflect:latest.integration")
        api("org.ow2.asm:asm:latest.integration")
        api("org.ow2.asm:asm-tree:latest.integration")

        compileOnly("org.jetbrains:annotations:latest.integration")
    }

    tasks.withType(compileKotlin.class) {
        kotlinOptions {
            jvmTarget = targetCompatibility
        }
    }

    java {
        sourceCompatibility = 16
        targetCompatibility = 16

        withSourcesJar()
    }

    jar {
        from(rootProject.file("LICENSE"))
    }

    publishing {
        repositories {
            maven {
                url = "https://maven.auoeke.net"

                credentials {
                    username = System.properties["maven.username"]
                    password = System.properties["maven.password"]
                }
            }
        }
    }

    if (plugin) {
        plugins.apply("java-gradle-plugin")

        sourceSets {
            test {
                kotlin.srcDir("test/src")
            }
        }

        dependencies {
            testImplementation(rootProject.sourceSets.test.output)
            testImplementation("org.junit.jupiter:junit-jupiter-api:latest.integration")
            testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
        }

        test {
            dependsOn(pluginUnderTestMetadata)
            useJUnitPlatform()
            debug = ManagementFactory.runtimeMXBean.inputArguments.any {it.contains("jdwp")}
        }

        gradlePlugin {
            plugins {
                "wheel-$project.name" {
                    id = name
                    implementationClass = "net.auoeke.wheel.Wheel${project.name.split("-").collect {it.capitalize()} join()}Plugin"
                }
            }
        }
    } else {
        publishing {
            publications {
                maven(MavenPublication) {
                    from(components.java)
                }
            }
        }
    }
}

subprojects {
    buildDir = "$rootProject.buildDir/$project.name"

    dependencies {
        api(rootProject)
    }
}

sourceSets {
    test {
        kotlin.srcDir("test")
    }
}

dependencies {
    testCompileOnly(gradleTestKit())
}
