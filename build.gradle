plugins {
    id("org.jetbrains.kotlin.jvm").version("latest.release")
}

allprojects {
    plugins.with {
        apply("maven-publish")
        apply("java-library")
        apply("org.jetbrains.kotlin.jvm")
    }

    group = "net.auoeke"
    version = "0-SNAPSHOT"

    sourceCompatibility = 16
    targetCompatibility = 16

    sourceSets {
        main {
            java.srcDir("src")
            kotlin.srcDir("src")
        }
    }

    repositories {
        mavenCentral()

        maven {url = "https://auoeke.jfrog.io/artifactory/maven"}
    }

    dependencies {
        api(gradleApi())
        api("net.gudenau.lib:unsafe:latest.integration")
        api("net.auoeke:reflect:latest.integration")
        api("org.ow2.asm:asm:latest.integration")
        api("org.ow2.asm:asm-tree:latest.integration")

        compileOnly("org.jetbrains:annotations:latest.integration")
    }

    tasks.withType(compileKotlin.class) {
        kotlinOptions {
            jvmTarget = targetCompatibility
        }
    }

    java {
        withSourcesJar()
    }

    jar {
        from(rootProject.file("LICENSE"))
    }

    publishing {
        repositories {
            maven {
                url = "https://maven.auoeke.net"

                credentials {
                    username = System.properties["maven.username"]
                    password = System.properties["maven.password"]
                }
            }
        }
    }

    if (plugin) {
        plugins.apply("java-gradle-plugin")

        pluginUnderTestMetadata {
            rootProject.test.dependsOn(it)

            doLast {
                def destinationDirectory = "$rootProject.buildDir/pluginMetadata" as File
                destinationDirectory.mkdirs()

                def destination = "${destinationDirectory}/$project.name-$METADATA_FILE_NAME" as File
                destination.delete()

                def output = "${outputDirectory.asFile.get()}/$METADATA_FILE_NAME" as File
                output.renameTo(destination)
            }
        }

        gradlePlugin {
            plugins {
                register("wheel-$project.name") {
                    id = name
                    implementationClass = "net.auoeke.wheel.Wheel${project.name.split("-").collect {it.capitalize()} join()}Plugin"
                }
            }
        }
    } else {
        publishing {
            publications {
                maven(MavenPublication) {
                    from(components.java)
                }
            }
        }
    }
}

subprojects {
    buildDir = "$rootProject.buildDir/$project.name"

    dependencies {
        api(rootProject)
    }
}

sourceSets {
    test {
        kotlin.srcDir("test/src")
    }
}

dependencies {
    testImplementation(gradleTestKit())
    testImplementation("org.junit.jupiter:junit-jupiter-api:latest.integration")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
}

test {
    useJUnitPlatform()
}
